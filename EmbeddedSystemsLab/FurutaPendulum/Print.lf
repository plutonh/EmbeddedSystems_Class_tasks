/**
 * A utility reactor to print the pendulum state into a csv file.
 */
 target C{
    flags: "-lm"
};

preamble {=
    #include <math.h>
    #define PI 3.14159265
    
    double sign(double x) {
            return (x > 0.0) - (x < 0.0); 
    }

    double restrictAngle(double theta) {
            return((fmod(fabs(theta) + PI, 2 * PI) - PI) * sign(theta));
    }
=}

// Check path of csv.
reactor Print(filename:string("/home/plutonh/project/EmbeddedSystemsLab/FurutaPendulum/pendulum.csv")) {
    input control:double;
    input theta:double;
    input phi:double;

    state file:FILE*({=NULL=});

    reaction(startup) {=
        self->file = fopen(self->filename, "w");
        if(self->file == NULL) {
            lf_print_error_and_exit("Failed to open file: %s", self->filename);
        } else {
            fprintf(self->file, "Time,Control,Theta,Phi\n");
        }
    =}

    reaction(control, theta, phi) {=
        double t = lf_time_logical_elapsed() / 1.0e9;
        fprintf(self->file,
            "%f,%f,%f,%f\n",
            t,
            control->value,
            restrictAngle(theta->value),
            restrictAngle(phi->value)
        );
    =}

    reaction(shutdown) {=
        fclose(self->file);
    =}
}